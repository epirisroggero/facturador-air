<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:articulos="biz.fulltime.ui.articulos.*" xmlns:s="library://ns.adobe.com/flex/spark"
		 width="100%" height="100%">
	<fx:Script>
		<![CDATA[
			import biz.fulltime.conf.ServerConfig;
			import biz.fulltime.event.ArticuloEvent;
			import biz.fulltime.model.Articulo;
			 
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.remoting.RemoteObject;
			
			private var _articuloSeleccionado:Articulo;
			
			[Bindable]
			public function get articuloSeleccionado():Articulo {
				return _articuloSeleccionado;
			}
			
			public function set articuloSeleccionado(value:Articulo):void {
				_articuloSeleccionado = value;
			}
			
			protected function hdividedbox1_creationCompleteHandler(event:FlexEvent):void {
				leftPanel.addEventListener(ArticuloEvent.ARTICULO_SELECCIONADO, cambioArticuloSeleccionado);
				rightPanel.addEventListener(ArticuloEvent.REFRESCAR_ARTICULO_SELECCIONADO, refrescarArticuloSeleccionado);
			}
			
			private function cambioArticuloSeleccionado(event:ArticuloEvent):void {
				loadArticulo(event.articuloDTO.codigo);
			}
			
			private function refrescarArticuloSeleccionado(event:ArticuloEvent):void {
				loadArticulo(event.articuloDTO.codigo);
			}
			
			private function loadArticulo(codigo:String):void {
				var articuloRemObj:RemoteObject = new RemoteObject();
				articuloRemObj.destination = "CreatingRpc";
				articuloRemObj.channelSet = ServerConfig.getInstance().channelSet;
				articuloRemObj.addEventListener(ResultEvent.RESULT, resultArticulo);
				articuloRemObj.addEventListener(FaultEvent.FAULT, handleFault);
				
				articuloRemObj.showBusyCursor = true;
				articuloRemObj.findCatalogEntity("Articulo", codigo);
			
			}
			
			private function resultArticulo(event:ResultEvent):void {
				var result:* = event.result;
				
				if (result is Articulo) {
					articuloSeleccionado = result as Articulo;
				}
				
			}
									
			private function handleFault(event:FaultEvent):void {
				var message:String = event.fault && event.fault.rootCause && event.fault.rootCause.cause ? event.fault.rootCause.cause.localizedMessage : null;
				if (!message) {
					message = event.message.toString();
				}				
				Alert.show(message, "Error");
			}
		]]>
	</fx:Script>
	
	<mx:HDividedBox width="100%" height="100%" creationComplete="hdividedbox1_creationCompleteHandler(event)">
		<articulos:ArticulosEditorLeftPanel id="leftPanel" width="340" height="100%" />
		<articulos:ArticulosEditorRightPanel id="rightPanel" width="100%" height="100%" articulo="{articuloSeleccionado}"/> 
	</mx:HDividedBox>
	
</s:Group>
