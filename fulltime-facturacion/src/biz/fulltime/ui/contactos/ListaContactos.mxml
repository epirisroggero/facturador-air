<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:components="biz.fulltime.ui.components.*" xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 width="100%" height="100%" xmlns:renderers="biz.fulltime.ui.renderers.*" xmlns:loader="biz.fulltime.util.loader.*">

	<fx:Script>
		<![CDATA[
			import biz.fulltime.conf.ServerConfig;
			import biz.fulltime.dto.ContactoDTO;
			import biz.fulltime.dto.ContactoQuery;
			import biz.fulltime.event.ContactoEvent;
			import biz.fulltime.model.Giro;
			import biz.fulltime.model.Zona;
			
			import com.hillelcoren.components.AutoComplete;
			
			import mx.collections.ArrayCollection;
			import mx.collections.Sort;
			import mx.collections.SortField;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.remoting.RemoteObject;
			
			import util.CatalogoFactory;

			[Bindable]
			private var contactos:ArrayCollection = new ArrayCollection();

			private var contacto:ContactoDTO;

			private var _loading:Boolean = false;
			
			
			[Bindable]
			public function get loading():Boolean {
				return _loading;
			}
			
			public function set loading(value:Boolean):void {
				_loading = value;
			}
			
			private function keyHandler(event:KeyboardEvent):void {
				if (event.keyCode == Keyboard.ENTER) {
					filterElements();
				} else if (filter.text == null || filter.text == "") {
					if (contactos != null) {
						contactos.filterFunction = null;
						contactos.refresh();
					}
				}
			}

			private function filterElements():void {
				if (contactos) {
					if (filter.text == null || filter.text == "") {
						contactos.filterFunction = null;
						contactos.refresh();
					} else {
						contactos.filterFunction = filtrarContactos;
						contactos.refresh();
					}
				}
			}

			private function filtrarContactos(item:Object):Boolean {
				var contacto:ContactoDTO = item as ContactoDTO;
				var _contactoLabel:String = contacto.codigo + ' ' + contacto.nombre + ' ' + contacto.razonSocial;
				return _contactoLabel.toUpperCase().match(new RegExp(filter.text.toUpperCase(), 'i')) != null;
			}

			protected function resetSearch(event:MouseEvent):void {
				btnCloseSearch.visible = false;
				if (contactos == null) {
					return;
				}
				contactos.filterFunction = null;
				contactos.refresh();

				if (contactos.length > 0) {
					listaContactos.invalidateList();
				}
				filter.text = '';
				filter.setFocus();
			}
			

			public function setNuevoContacto():void {
				var contactoObj:RemoteObject = new RemoteObject();
				contactoObj.destination = "CreatingRpc";
				contactoObj.channelSet = ServerConfig.getInstance().channelSet;
				contactoObj.addEventListener(ResultEvent.RESULT, function(event:ResultEvent):void {
					var code:Number = new Number(event.result.toString());
					
					var contacto:ContactoDTO = new ContactoDTO();
					contacto.codigo = (code + 1).toString();
					contacto.nombre = "";

					callLater(function():void {
						dispatchEvent(new ContactoEvent(ContactoEvent.CONTACTO_NUEVO, null, contacto));
					});
					
				});
				contactoObj.addEventListener(FaultEvent.FAULT, handleFault);					
				contactoObj.showBusyCursor = true;				
				contactoObj.getLastId();
				
			}

			public function removerNuevoContacto():void {
				callLater(function():void {
					var selected:ContactoDTO = listaContactos.selectedItem as ContactoDTO;
					dispatchEvent(new ContactoEvent(ContactoEvent.CONTACTO_SELECCIONADO, null, selected));
				});
			}

			private function showButton(evt:Event):void {
				btnCloseSearch.visible = filter.text.length > 0;
			}

			protected function listaContactos_creationCompleteHandler(event:FlexEvent):void {
				refrescarContactos();
				listaContactos.addEventListener(ContactoEvent.BORRAR_CONTACTO, borrarContacto);
			}
			
			
			private function borrarContacto(event:ContactoEvent):void {
				contacto = event.contactoDTO;
				
				var contactoRemObj:RemoteObject = new RemoteObject();
				contactoRemObj.destination = "CreatingRpc";
				contactoRemObj.channelSet = ServerConfig.getInstance().channelSet;
				contactoRemObj.addEventListener(ResultEvent.RESULT, function(event:ResultEvent):void {
					var selectedIndex:int = listaContactos.selectedIndex;
					var selected:ContactoDTO = null;

					if (selectedIndex == 0) {
						selected = ArrayCollection(listaContactos.dataProvider).getItemAt(selectedIndex) as ContactoDTO;
					} else {
						selected = ArrayCollection(listaContactos.dataProvider).getItemAt(selectedIndex - 1) as ContactoDTO;
					}
					refrescarContactos(null, selected);
				});
				contactoRemObj.addEventListener(FaultEvent.FAULT, handleFault);					
				contactoRemObj.showBusyCursor = true;
				contactoRemObj.borrarContacto(contacto.codigo);
			}
			
			private function handleFault(event:FaultEvent):void {
				var message:String = event.fault && event.fault.rootCause && event.fault.rootCause.localizedMessage ? event.fault.rootCause.localizedMessage : null;
				if (!message) {
					message = event.message.toString();
				}				
				Alert.show(message, "Error", 4, null, null, StyleManager.getStyleManager(null).getStyleDeclaration('.icons32').getStyle('ErrorIcon'));
			}

			public function refrescarContactos(event:Event = null, select:ContactoDTO = null):void {
				filter.text = "";
				contactos = new ArrayCollection();
				
				loading = true;

				var contactoRemObj:RemoteObject = new RemoteObject();
				contactoRemObj.destination = "CreatingRpc";
				contactoRemObj.channelSet = ServerConfig.getInstance().channelSet;
				contactoRemObj.addEventListener(ResultEvent.RESULT, function(event:ResultEvent):void {
					contactos = event.result as ArrayCollection;
					listaContactos.dataProvider = sort(contactos, true);
					
					loading = false;
					
					if (contactos.length > 0) {
						var index:Number = 0;
						if (select) {
							for each (var cli:ContactoDTO in listaContactos.dataProvider) {
								if (cli.codigo == select.codigo) {
									break;
								}
								index++
							}
						}
						listaContactos.selectedIndex = index;	
						callLater(function():void {
							listaContactos.scrollToIndex(index);
						});
						var contactoDTO:ContactoDTO = ArrayCollection(listaContactos.dataProvider).getItemAt(index) as ContactoDTO;
						dispatchEvent(new ContactoEvent(ContactoEvent.CONTACTO_SELECCIONADO, null, contactoDTO));
					}

				});
				contactoRemObj.addEventListener(FaultEvent.FAULT, handleFault);					
				contactoRemObj.showBusyCursor = true;
				
				var query:ContactoQuery = new ContactoQuery();

				if (cmbZona.selectedItem) {
					query.zona = (cmbZona.selectedItem as Zona).codigo;	
				} else {
					query.zona = null;
				}
				if (cmbGiro.selectedItem) {
					query.giro = (cmbGiro.selectedItem as Giro).codigo;	
				} else {
					query.giro = null;
				}				
				query.activo = chbActivos.selected;
				
				contactoRemObj.queryContactos(query);

				
			}
			
			private function sort(arrColl:ArrayCollection, isNumeric:Boolean = true):ArrayCollection {
				/* Create the SortField object for the "data" field in the ArrayCollection object, and make sure we do a numeric sort. */
				var dataSortField:SortField = new SortField();
				dataSortField.name = "codigo";
				dataSortField.numeric = isNumeric;
				
				/* Create the Sort object and add the SortField object created earlier to the array of fields to sort on. */
				var numericDataSort:Sort = new Sort();
				numericDataSort.fields = [dataSortField];
				
				/* Set the ArrayCollection object's sort property to our custom sort, and refresh the ArrayCollection. */
				arrColl.sort = numericDataSort;
				arrColl.refresh();
				
				return arrColl;
			}


			protected function listaContactos_changeHandler(event:ListEvent):void {
				var contactoDTO:ContactoDTO = listaContactos.selectedItem as ContactoDTO;
				
				dispatchEvent(new ContactoEvent(ContactoEvent.CONTACTO_SELECCIONADO, null, contactoDTO));
			}

			protected function filter_creationCompleteHandler(event:FlexEvent):void {
				filter.addEventListener(KeyboardEvent.KEY_UP, keyHandler);
			}
			
			protected function resetSearchEsp(event:MouseEvent):void {
			}
			
			public function codigoNombreLabelFunction(item:Object):String {
				var label:String;
				if (item && item.hasOwnProperty("codigo")) {
					label = item.codigo;
				}
				if (item && item.hasOwnProperty("nombre")) {
					label += " - " + item.nombre;
				}
				return label;
			}
			
			public function getContactos():ArrayCollection {
				var c:ArrayCollection = new ArrayCollection();
				for each (var p:ContactoDTO in listaContactos.dataProvider) {
					c.addItem(p);
				}
				return c;
			}


			protected function cmdFiltrar_clickHandler(event:MouseEvent):void {
				refrescarContactos();
				
			}
			
			protected function cmdLimpiar_clickHandler(event:MouseEvent):void {
				limpiarFiltos();
			}

			public function limpiarFiltos():void {
				cmbZona.clear();
			}


			protected function cmdExcel_clickHandler(event:MouseEvent):void {
				dispatchEvent(new Event("exportToExcel", true, true));
			}
			
			protected function cmdAgregar_clickHandler(event:MouseEvent):void {
				dispatchEvent(new Event("addNewClient", true, true));
				
			}
			
			protected function btnSearch_clickHandler(event:MouseEvent):void {
				filterElements();
				
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<fx:Component id="dropDownItemRenderer">
			<renderers:CodigoNombreItemRenderer />
		</fx:Component>
		
	</fx:Declarations>
	
	<s:Panel title="Filtros" width="100%" dropShadowVisible="false">		
		<s:layout>
			<s:VerticalLayout paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5"/>
		</s:layout>
		
		<s:HGroup width="100%">
			<s:VGroup width="100%" paddingBottom="0" paddingLeft="0" paddingRight="0" verticalAlign="middle" gap="0">
				<s:Label fontSize="12" text="ZONA" width="100%" left="5"/>
				<components:MyAutoComplete id="cmbZona" width="100%" textAlign="left"
										   dataProvider="{ CatalogoFactory.getInstance().zonas }" labelField="nombre" matchType="anyPart" prompt="No Filtrar"
										   allowNewValues="false" selectedItemStyleName="{ AutoComplete.STYLE_MAC_MAIL }" allowDuplicates="false"
										   allowMultipleSelection="false" allowEditingNewValues="false" labelFunction="codigoNombreLabelFunction"
										   backspaceAction="focus" next_focus="{cmbGiro}"
										   dropDownItemRenderer="{ dropDownItemRenderer }" />
			</s:VGroup>			
		</s:HGroup>
		<s:HGroup width="100%">			
			<s:VGroup width="100%" paddingBottom="0" paddingLeft="0" paddingRight="0" verticalAlign="middle" gap="0" >
				<s:Label fontSize="12" text="GIRO" width="100%" left="5"/>
				<components:MyAutoComplete id="cmbGiro" width="100%" textAlign="left"
										   dataProvider="{ CatalogoFactory.getInstance().giros }" labelField="nombre" matchType="anyPart" prompt="No Filtrar"
										   allowNewValues="false" selectedItemStyleName="{ AutoComplete.STYLE_MAC_MAIL }" allowDuplicates="false"
										   allowMultipleSelection="false" allowEditingNewValues="false" labelFunction="codigoNombreLabelFunction"
										   backspaceAction="focus" previous_focus="{cmbZona}"
										   dropDownItemRenderer="{ dropDownItemRenderer }" />
			</s:VGroup>
		</s:HGroup>	


		<s:controlBarContent>
			<s:HGroup width="100%" horizontalAlign="right">
				<s:CheckBox id="chbActivos" label="Activos" selected="true" />
				<s:Spacer width="100%" />
				<s:Button id="cmdFiltrar" click="cmdFiltrar_clickHandler(event)" label="Filtrar" enabled="true" toolTip="Filtrar" styleName="filtrarButton" />
				<s:Button id="cmdLimpiar" click="cmdLimpiar_clickHandler(event)" label="Limpiar" toolTip="Limpiar Todos los Campos" styleName="clearButton16" />
			</s:HGroup>
		</s:controlBarContent>

	</s:Panel>
		
	<s:Panel width="100%" height="100%" title="Resultado" dropShadowVisible="false">
		<s:layout>
			<s:VerticalLayout paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5"/>
		</s:layout>
		<s:HGroup width="100%" paddingBottom="0" paddingLeft="0" paddingRight="0" verticalAlign="middle" gap="5">
			<s:Label id="lblFilter" fontSize="12" text="Buscar: " left="5"/>
			<s:SkinnableContainer width="100%" styleName="filterTextInput">
				<s:layout>
					<s:HorizontalLayout horizontalAlign="center" paddingBottom="1" paddingLeft="2" paddingRight="5" paddingTop="1" verticalAlign="middle" />
				</s:layout>
				<s:TextInput id="filter" width="100%" change="showButton(event)" creationComplete="filter_creationCompleteHandler(event)" styleName="filter" prompt="Ver todos"/>
				<s:Button id="btnCloseSearch" width="8" height="8" click="resetSearch(event)" styleName="buttonCloseSearch" visible="false" />
				<s:Button id="btnSearch" width="16" height="16" click="btnSearch_clickHandler(event)" styleName="buttonSearch"/>
			</s:SkinnableContainer>
		</s:HGroup>			
		
		<s:Group width="100%" height="100%">
			<s:VGroup width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" visible="{loading}" includeInLayout="{loading}">
				<loader:LoadComponent label="Cargando"/>
			</s:VGroup>			
			<s:VGroup width="100%" height="100%" visible="{!loading}" includeInLayout="{!loading}">
				<components:RoundedSelectionList id="listaContactos" width="100%" height="100%" borderSkin="{null}" selectionColor="0xfafafa" change="listaContactos_changeHandler(event)"
												 creationComplete="listaContactos_creationCompleteHandler(event)" dataProvider="{contactos}" selectionDuration="600" useRollOver="false"
												 itemRenderer="biz.fulltime.ui.components.ContactoLabel" />
				
			</s:VGroup>
			
		</s:Group>
		
		<s:controlBarContent>
			<s:HGroup width="100%" horizontalAlign="right" verticalAlign="middle" paddingBottom="0" paddingTop="0">
				<s:Button id="cmdExcel" click="cmdExcel_clickHandler(event)" label="Excel" styleName="exportExcel" />
				<s:Spacer width="100%" />
				<s:Button id="cmdAgregar" click="cmdAgregar_clickHandler(event)" label="Nuevo Contacto" styleName="agregarContacto" height="32"/>
			</s:HGroup>
		</s:controlBarContent>

	</s:Panel>
		
</s:VGroup>
