<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:personas="biz.fulltime.ui.personas.*" xmlns:s="library://ns.adobe.com/flex/spark"
		 width="100%" height="100%">
	<fx:Script>
		<![CDATA[
			import biz.fulltime.conf.ServerConfig;
			import biz.fulltime.dto.ClienteDTO;
			import biz.fulltime.event.ClienteEvent;
			import biz.fulltime.model.Cliente;

			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.rpc.remoting.RemoteObject;

			private var _clienteSeleccionado:Cliente = null;

			[Bindable]
			public function get clienteSeleccionado():Cliente {
				return _clienteSeleccionado;
			}

			public function set clienteSeleccionado(value:Cliente):void {
				_clienteSeleccionado = value;
			}

			protected function hdividedbox1_creationCompleteHandler(event:FlexEvent):void {
				leftPanel.addEventListener(ClienteEvent.CLIENTE_SELECCIONADO, cambioClienteSeleccionado);
				leftPanel.addEventListener(ClienteEvent.CLIENTE_NUEVO, nuevoCliente);

				rightPanel.addEventListener(ClienteEvent.CANCELAR_EDICION, function(evt:ClienteEvent):void {
					leftPanel.visible = true;
					leftPanel.includeInLayout = true;

					leftPanel.cancelar();
				});
				
				rightPanel.addEventListener(ClienteEvent.FINALIZAR_EDICION, function(evt:ClienteEvent):void {
					if (leftPanel.personsList.listaClientes.selectedItem) {
						leftPanel.personsList.listaClientes.selectedItem.nombre = evt.clienteDTO.nombre; 
					}
				});

				rightPanel.addEventListener(ClienteEvent.CLIENTE_NUEVO, function(evt:ClienteEvent):void {
					leftPanel.visible = true;
					leftPanel.includeInLayout = true;

					leftPanel.finalizar(evt.clienteDTO);
				});
				
				rightPanel.addEventListener("changeClientState", function(evt:ClienteEvent):void {
					if (leftPanel.personsList.listaClientes.selectedItem) {
						leftPanel.personsList.listaClientes.selectedItem.activo = evt.cliente.contacto.ctoActivo == "S";
					}
				});

			}

			private function cambioClienteSeleccionado(event:ClienteEvent):void {
				var clienteRemObj:RemoteObject = new RemoteObject();
				clienteRemObj.destination = "CreatingRpc";
				clienteRemObj.channelSet = ServerConfig.getInstance().channelSet;
				clienteRemObj.addEventListener(ResultEvent.RESULT, resultCliente);
				clienteRemObj.addEventListener(FaultEvent.FAULT, handleFault);

				clienteRemObj.showBusyCursor = true;
				clienteRemObj.findCatalogEntity("Cliente", event.clienteDTO.codigo);
			}

			private function resultCliente(event:ResultEvent):void {
				var result:* = event.result;

				if (result is Cliente) {
					var client:Cliente = result as Cliente;

					callLater(function():void {
						client.esNuevo = false;
						clienteSeleccionado = client;
					});

					var personaRemObj:RemoteObject = new RemoteObject();
					personaRemObj.destination = "CreatingRpc";
					personaRemObj.channelSet = ServerConfig.getInstance().channelSet;
					personaRemObj.addEventListener(ResultEvent.RESULT, resultPersonas);
					personaRemObj.addEventListener(FaultEvent.FAULT, handleFault);

					personaRemObj.showBusyCursor = true;
					personaRemObj.getPersonas(client.codigo);

				}

			}

			[Bindable]
			private var personas:ArrayCollection;

			private function resultPersonas(event:ResultEvent):void {
				var result:* = event.result;

				personas = result as ArrayCollection;
			}

			private function nuevoCliente(event:ClienteEvent):void {
				var c:ClienteDTO = event.clienteDTO;

				var client:Cliente = new Cliente();

				client.codigo = c.codigo;
				client.nombre = c.nombre;
				client.esNuevo = true;

				leftPanel.visible = false;
				leftPanel.includeInLayout = false;

				rightPanel.setNuevoCliente(client);
			}

			private function handleFault(event:FaultEvent):void {
				var message:String = event.fault && event.fault.rootCause && event.fault.rootCause.cause ? event.fault.rootCause.cause.localizedMessage : null;
				if (!message) {
					message = event.message.toString();
				}				
				Alert.show(message, "Error", 4, null, null, StyleManager.getStyleManager(null).getStyleDeclaration('.icons32').getStyle('ErrorIcon'));
			}
		]]>
	</fx:Script>

	<mx:HDividedBox width="100%" height="100%" creationComplete="hdividedbox1_creationCompleteHandler(event)">
		<personas:ClienteEditorLeftPanel id="leftPanel" width="320" height="100%" />
		<personas:ClienteEditorRightPanel id="rightPanel" width="100%" height="100%" cliente="{clienteSeleccionado}" personas="{personas}" />
	</mx:HDividedBox>

</s:Group>
