<?xml version="1.0" encoding="utf-8"?>
<components2:PanelShowHide xmlns:components="com.hillelcoren.components.*" xmlns:components1="biz.fulltime.ui.components.*" xmlns:components2="components.*"
						   xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:renderers="biz.fulltime.ui.renderers.*"
						   xmlns:s="library://ns.adobe.com/flex/spark" creationComplete="creationCompleteHandler(event)"
						   backgroundAlpha=".98" backgroundColor="0xfafafa" title="Filtrar por">

	<fx:Script>
		<![CDATA[
			import biz.fulltime.dto.DocumentoQuery;
			import biz.fulltime.event.ClienteEvent; 
			import biz.fulltime.event.FilterEvent;
			import biz.fulltime.model.Cliente;
			import biz.fulltime.model.Comprobante;
			import biz.fulltime.model.Proveedor;
			import biz.fulltime.ui.components.HelpClientPnl;
			
			import com.hillelcoren.components.AutoComplete;
			import com.hillelcoren.utils.StringUtils;
			
			import mx.collections.ArrayCollection;
			import mx.core.IFlexDisplayObject;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import spark.components.TitleWindow;
			
			import util.CatalogoFactory;

			private var helpWindow:TitleWindow;

			private var _docQuery:DocumentoQuery;
			
			private var _soloCotizaciones:Boolean;
			
			private var _soloPendientes:Boolean = true;
			
			private var _modoSolicitudes:Boolean;

			public function get soloPendientes():Boolean {
				return _soloPendientes;
			}

			[Bindable]
			public function set soloPendientes(value:Boolean):void {
				_soloPendientes = value;
			}

			[Bindable]
			public function get modoSolicitudes():Boolean {
				return _modoSolicitudes;
			}

			public function set modoSolicitudes(value:Boolean):void {
				_modoSolicitudes = value;
			}

			[Bindable]
			public function get soloCotizaciones():Boolean {
				return _soloCotizaciones;
			}

			public function set soloCotizaciones(value:Boolean):void {
				_soloCotizaciones = value;
			}

			public function get docQuery():DocumentoQuery {
				return _docQuery;
			}

			public function set docQuery(value:DocumentoQuery):void {
				_docQuery = value;
			}

			protected function cmdFilter_clickHandler(event:MouseEvent):void {
				var serie:String = txtSerie.text;
				var nro:String = txtNumero.text;

				var codTipoComp:String = acTipoComprobante.selectedItem != null ? acTipoComprobante.selectedItem.codigo : null;
				var codCli:String = acCliente.selectedItem != null ? acCliente.selectedItem.codigo : null;
				var codPrv:String = acProveedor.selectedItem != null ? acProveedor.selectedItem.codigo : null;

				var fechaDesde:Date = txtFechaDesde.selectedDate;
				var fechaHasta:Date = txtFechaHasta.selectedDate;
				var lineaConcepto:String = txtConcepto.text;

				_docQuery = new DocumentoQuery();
				
				soloPendientes = chbPendientes.selected;
				
				_docQuery.pendiente = chbPendientes.selected;

				if (lineaConcepto && lineaConcepto != "") {
					_docQuery.lineaConcepto = lineaConcepto;
				} else {
					_docQuery.lineaConcepto = null;
				}
				if (serie && serie != "") {
					_docQuery.serie = serie;
				} else {
					_docQuery.serie = null;
				}
				if (nro && nro != "") {
					_docQuery.numero = nro;
				} else {
					_docQuery.numero = null;
				}

				_docQuery.comprobantes = acTipoComprobante.selectedItem != null ? acTipoComprobante.selectedItem.codigo : null;
				
				_docQuery.proveedor = null;
				_docQuery.cliente = null;
				
				if (modoSolicitudes) {
					if (codPrv && codPrv != "") {
						_docQuery.proveedor = codPrv;
					} 
				} else {
					if (codCli && codCli != "") {
						_docQuery.cliente = codCli;
					} 
				}
				
				_docQuery.esSolicitud = modoSolicitudes;
				
				if (fechaDesde) {
					_docQuery.fechaDesde = fechaDesde;
				} else {
					_docQuery.fechaDesde = null;
				}
				if (fechaHasta) {
					_docQuery.fechaHasta = fechaHasta;
				} else {
					_docQuery.fechaHasta = null;
				}
				dispatchEvent(new FilterEvent(FilterEvent.FILTRAR_DOCUMENTOS, false, false, _docQuery));
			}

			protected function cmdLimpiar_clickHandler(event:MouseEvent):void {
				txtFechaDesde.data = null;
				txtFechaHasta.data = null;

				txtNumero.text = "";
				txtSerie.text = "";
				txtConcepto.text = "";

				if (modoSolicitudes) {
					acProveedor.clear();
				} else {
					acCliente.clear();
				}
				acTipoComprobante.clear();

				var _docQuery1:DocumentoQuery = new DocumentoQuery();
				_docQuery1.pendiente = chbPendientes.selected;
				_docQuery1.esSolicitud = modoSolicitudes;
				
				var evtFilter:FilterEvent = new FilterEvent(FilterEvent.BORRAR_FILTROS);
				evtFilter.docQuery = _docQuery1;
				dispatchEvent(evtFilter);
			}

			public function dropDownLabelFunction(item:Object):String {
				var string:String = item.nombre + " &lt;" + item.codigo + "&gt;";
				var searchStr:String = acTipoComprobante.searchText;

				var returnStr:String = StringUtils.highlightMatch(string, searchStr);

				if (acTipoComprobante.selectedItems.getItemIndex(item) >= 0) {
					returnStr = "<font color='" + Consts.COLOR_TEXT_DISABLED + "'>" + returnStr + "</font>";
				}

				return returnStr;
			}

			public function codigoNombreLabelFunction(item:Object):String {
				var lblCliente:String;

				if (item != null && item.hasOwnProperty("codigo")) {
					lblCliente = item.codigo;
				}
				if (item != null && item.hasOwnProperty("nombre")) {
					lblCliente += " - " + item.nombre;
				}
				return lblCliente;
			}

			
			public function clienteLabelFunction(item:Object):String {
				var client:Cliente = item as Cliente;
				
				var label:String;
				if (item && item.hasOwnProperty("codigo")) {
					label = item.codigo;
				}
				if (item && item.hasOwnProperty("nombre")) {
					label += " - " + item.nombre;
				}
				if (client.razonSocial != client.nombre) {
					label += " [" + client.razonSocial + "] ";
				}
				
				return label;
			}
			
			public function proveedorLabelFunction(item:Object):String {
				var proveedor:Proveedor = item as Proveedor;
				
				var label:String;
				if (item && item.hasOwnProperty("codigo")) {
					label = item.codigo;
				}
				if (item && item.hasOwnProperty("nombre")) {
					label += " - " + item.nombre;
				}
				if (proveedor.contacto.ctoRSocial != proveedor.contacto.nombre) {
					label += " [" + proveedor.contacto.ctoRSocial + "] ";
				}
				
				return label;
			}


			private function capture_keyDownHandler(event:KeyboardEvent):void {
				if (event.keyCode == Keyboard.F1) {
					cmbClient_helpFTHandler();
				}
			}

			//
			// Seleccion de Cliente
			// 
			protected function cmbClient_helpFTHandler(event:Event = null):void {
				if (helpWindow == null) {
					helpWindow = new TitleWindow();
					helpWindow.title = "Clientes";
					helpWindow.width = 840;
					helpWindow.height = 670;

					var hlpPanel:HelpClientPnl = new HelpClientPnl();

					PopUpManager.addPopUp(helpWindow, this, true);
					PopUpManager.centerPopUp(helpWindow);

					helpWindow.addEventListener(CloseEvent.CLOSE, cliente_closeHandler);
					hlpPanel.addEventListener(CloseEvent.CLOSE, cliente_closeHandler);
					hlpPanel.addEventListener(ClienteEvent.CLIENTE_SELECCIONADO, clienteSeleccionado);

					helpWindow.addElement(hlpPanel);
				}
			}

			private function clienteSeleccionado(event:ClienteEvent):void {
				helpWindow.removeEventListener(CloseEvent.CLOSE, cliente_closeHandler);
				PopUpManager.removePopUp(helpWindow as IFlexDisplayObject);
				helpWindow = null;

				var cliente:Cliente = event.cliente;
				acCliente.selectedItem = cliente;
			}

			private function cliente_closeHandler(event:Event):void {
				helpWindow.removeEventListener(CloseEvent.CLOSE, cliente_closeHandler);
				PopUpManager.removePopUp(helpWindow as IFlexDisplayObject);
				helpWindow = null;

				callLater(function():void {
					acCliente.setFocus();
				});
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				var comprobantes:ArrayCollection = new ArrayCollection();
				for each (var i:Comprobante in CatalogoFactory.getInstance().comprobantes) {
					if (modoSolicitudes) {
						if (i.tipo == Comprobante.COMPRA_CREDITO || i.tipo == Comprobante.COMPRA_CONTADO || i.tipo == Comprobante.MOVIMIENTO_DE_STOCK_DE_PROVEEDOR) {
							comprobantes.addItem(i);
						}
					} else {
						if (i.tipo != Comprobante.COMPRA_CREDITO && i.tipo != Comprobante.COMPRA_CONTADO && i.tipo != Comprobante.MOVIMIENTO_DE_STOCK_DE_PROVEEDOR) {
							comprobantes.addItem(i);
						}
					}
				}
				acTipoComprobante.dataProvider = comprobantes;
			}
			
		]]>
	</fx:Script>

	<fx:Declarations>
		<fx:Component id="dropDownItemRenderer">
			<renderers:CodigoNombreItemRenderer selectedItems="{ outerDocument.acTipoComprobante.selectedItems }" />
		</fx:Component>
	</fx:Declarations>


	<s:VGroup width="100%" height="100%">
		<s:HGroup height="100%" paddingBottom="0">
			<mx:Form visible="{!soloCotizaciones}" includeInLayout="{!soloCotizaciones}">
				<mx:FormItem label="Serie" fontSize="13" fontWeight="normal">
					<mx:TextInput id="txtSerie" width="90" enter="txtNumero.setFocus()" />
				</mx:FormItem>
				<mx:FormItem label="NÃºmero" fontSize="13" fontWeight="normal">
					<s:TextInput id="txtNumero" width="90" restrict="0-9" enter="acTipoComprobante.setFocus()" />
				</mx:FormItem>
			</mx:Form>
			<mx:Form>
				<mx:FormItem label="Comprobante" visible="{!soloCotizaciones}" includeInLayout="{!soloCotizaciones}" fontSize="13" fontWeight="normal">
					<components1:MyAutoComplete id="acTipoComprobante" width="260" labelField="nombre"
												matchType="anyPart" prompt="Filtrar por Comprobante" allowNewValues="false" selectedItemStyleName="{ AutoComplete.STYLE_MAC_MAIL }"
												allowDuplicates="{ false }" allowMultipleSelection="{ false }" allowEditingNewValues="{ false }"
												dropDownItemRenderer="{ dropDownItemRenderer }" labelFunction="codigoNombreLabelFunction" 
												backspaceAction="focus" />
				</mx:FormItem>
				<mx:FormItem label="Cliente" visible="{!modoSolicitudes}" includeInLayout="{!modoSolicitudes}" fontSize="13" fontWeight="normal">
					<components1:MyAutoComplete id="acCliente" width="260" keyDown="capture_keyDownHandler(event)" 
												labelField="nombre" matchType="anyPart" prompt="Filtrar por Cliente" allowNewValues="false"
												selectedItemStyleName="{ AutoComplete.STYLE_MAC_MAIL }" allowDuplicates="{ false }" allowMultipleSelection="{ false }"
												allowEditingNewValues="{ false }" dropDownItemRenderer="{ dropDownItemRenderer }" labelFunction="clienteLabelFunction"
												backspaceAction="focus" dataProvider="{CatalogoFactory.getInstance().clientes}"/>
				</mx:FormItem>
				<mx:FormItem label="Proveedor" visible="{modoSolicitudes}" includeInLayout="{modoSolicitudes}" fontSize="13" fontWeight="normal">
					<components1:MyAutoComplete id="acProveedor" width="260" 
												labelField="nombre" matchType="anyPart" prompt="Filtrar por Proveedor" allowNewValues="false"
												selectedItemStyleName="{ AutoComplete.STYLE_MAC_MAIL }" allowDuplicates="{ false }" allowMultipleSelection="{ false }"
												allowEditingNewValues="{ false }" dropDownItemRenderer="{ dropDownItemRenderer }" labelFunction="proveedorLabelFunction"
												backspaceAction="focus" dataProvider="{CatalogoFactory.getInstance().proveedores}"/>
				</mx:FormItem>

			</mx:Form>
			<mx:Form>
				<mx:FormItem label="Fecha desde" fontSize="13" fontWeight="normal">
					<mx:DateField id="txtFechaDesde" width="110" yearNavigationEnabled="true" focusEnabled="true" formatString="DD/MM/YYYY" locale="es" fontFamily="Helvetica"/>
				</mx:FormItem>
				<mx:FormItem label="Fecha hasta" fontSize="13" fontWeight="normal">
					<mx:DateField id="txtFechaHasta" width="110" yearNavigationEnabled="true" focusEnabled="true" formatString="DD/MM/YYYY" locale="es" fontFamily="Helvetica"/>
				</mx:FormItem>
			</mx:Form> 
			<s:VGroup paddingTop="20" visible="{!soloCotizaciones}" includeInLayout="{!soloCotizaciones}">
				<s:CheckBox id="chbPendientes" label="Pendientes y/o NO emitidos" selected="true" fontSize="13" fontWeight="normal"/>
				<s:HGroup width="100%" verticalAlign="middle" paddingTop="10">
					<s:Label text="Concepto" />
					<s:TextInput id="txtConcepto" width="200" />
				</s:HGroup>
			</s:VGroup>
			
		</s:HGroup>
	</s:VGroup>
	<components2:controlBarContent>
		<s:HGroup width="100%" horizontalAlign="right" paddingBottom="0" paddingTop="0">
			<mx:Image id="loader1" source="@Embed(source='assets/general/logo_oscuro.gif')" alpha=".5" scaleX=".25" scaleY=".25" />
			<s:Spacer width="100%" />
			<s:Button id="cmdFiltrar" click="cmdFilter_clickHandler(event)" label="Filtrar" enabled="true" toolTip="Filtrar Facturas" styleName="filtrarButton" />
			<s:Button id="cmdLimpiar" click="cmdLimpiar_clickHandler(event)" label="Limpiar" toolTip="Limpiar Todos los Campos" styleName="clearButton16" />
		</s:HGroup>
	</components2:controlBarContent>
</components2:PanelShowHide>
